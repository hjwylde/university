apply from: 'dependencies.gradle'

buildscript {
    repositories {
        maven { url 'https://github.com/ben-manes/gradle-versions-plugin/raw/mvnrepo' }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-1'
    }
}

allprojects {
    apply plugin: 'versions'
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'checkstyle'

    defaultTasks 'build'

    sourceCompatibility = '1.7'

    jar {
        // Make the jars contain all the dependencies so the lecturers can run them
        from {
            configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task testSourcesJar(type: Jar, dependsOn: classes) {
        appendix = 'test'
        classifier = 'sources'
        from sourceSets.test.allSource
    }

    artifacts {
        archives sourcesJar
        archives testSourcesJar
    }

    checkstyle {
        configFile new File(rootDir, '/config/checkstyle/checkstyle.xml')
    }
}

task wrapper(type: Wrapper) {
    group 'Help'
    description 'Generates the Gradle wrapper files'

    gradleVersion = '1.11'
}

